"Filed out from Dolphin Smalltalk"!

Renderer subclass: #Renderer2D
	instanceVariableNames: 'sdlRenderer'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''!

Renderer2D guid: (GUID fromString: '{709e7faa-1d4b-4ee6-b0d9-077aa539b165}')!

Renderer2D comment: ''!

!Renderer2D categoriesForClass!Kernel-Objects! !

!Renderer2D methodsFor!

clear
	sdlRenderer clear!

clear: aColor
	sdlRenderer
		setDrawColor: aColor;
		clear!

createStaticTexture: aPoint
	^sdlRenderer createTexture: aPoint access: SDL_TEXTUREACCESS_STATIC!

createStreamingTexture: aPoint
	^sdlRenderer createTexture: aPoint access: SDL_TEXTUREACCESS_STREAMING!

createTargetTexture: aPoint
	^sdlRenderer createTexture: aPoint access: SDL_TEXTUREACCESS_TARGET!

createTextEngine
	textEngine := SDL3TTFLibrary default createRendererTextEngine_renderer: sdlRenderer!

createTexture: aPoint 
	^self createStreamingTexture: aPoint!

createTexture: aPoint format: anInteger
	^self createStreamingTexture: aPoint!

createTextureFromSurface: aSDL_Surface
	^sdlRenderer createTextureFromSurface: aSDL_Surface!

destroy
	sdlRenderer ifNil: [^self].
	sdlRenderer destroy.
	sdlRenderer := nil!

direct3DDevice
	^sdlRenderer direct3DDevice!

downloadTexture: aSDLTexture pixelFormat: anInteger
	| extent newTexture |
	extent := aSDLTexture extent.
	newTexture := self createTargetTexture: extent.
	sdlRenderer
		setRenderTarget: newTexture;
		setDrawColor: Color white;
		renderTexture: aSDLTexture.
	^sdlRenderer renderReadPixels: nil!

present: aCanvas
	| drawQueue |
	self clear: Color white.
	drawQueue := aCanvas drawQueue.
	drawQueue reverseDo: [:each | self queueDrawCommand: each].
	sdlRenderer present.
	drawQueue clear!

queueDrawCommand: aRenderCommand
	| selector |
	selector := CommandMap at: aRenderCommand class.
	aRenderCommand clip ifNotNil: [:aRectangle | self setClip: aRectangle].
	self perform: selector with: aRenderCommand.
	aRenderCommand clip ifNotNil: [self resetClip]!

queueDrawLine: aDrawLine
	aDrawLine color ifNotNil: [:aColor | sdlRenderer setDrawColor: aColor].
	sdlRenderer renderLineFrom: aDrawLine start to: aDrawLine end!

queueDrawPoint: aDrawPoint
	aDrawPoint color ifNotNil: [:aColor | sdlRenderer setDrawColor: aColor].
	sdlRenderer renderPoint: aDrawPoint position!

queueDrawRectangle: aDrawRectangle
	aDrawRectangle color ifNotNil: [:aColor | sdlRenderer setDrawColor: aColor].
	sdlRenderer renderRectangle: aDrawRectangle rectangle!

queueDrawString: aDrawString
	| font rectangle text |
	aDrawString string isString ifFalse: [^self].
	font := aDrawString font ifNil: [self defaultFont].
	rectangle := aDrawString rectangle.
	font setStyle: (aDrawString fontStyle ifNil: [TTFFontStyle normal]).
	text := textEngine createText: aDrawString string font: font.
	rectangle width > 0 ifTrue: [text wrapWidth: rectangle width asInteger].
	text color: (aDrawString color ifNil: [Color black]) asSDLColor.
	textEngine
		drawText: text
		x: rectangle origin x
		y: rectangle origin y!

queueDrawSurface: aDrawSurface
	| surface texture destinationRectangle |
	surface := aDrawSurface surface.
	surface ifNil: [^self].
	texture := self createTextureFromSurface: surface.
	destinationRectangle := aDrawSurface stretch
				ifTrue: [aDrawSurface rectangle]
				ifFalse: 
					[| textureExtent |
					textureExtent := texture getExtent.
					Rectangle origin: aDrawSurface rectangle origin extent: textureExtent].
	sdlRenderer
		renderTexture: texture
		source: nil
		destination: destinationRectangle!

queueDrawText: aDrawText
	| text rectangle |
	text := aDrawText text.
	text ifNil: [^self].
	rectangle := aDrawText rectangle.
	text color: (aDrawText color ifNil: [Color black]) asSDLColor.
	textEngine
		drawText: text
		x: rectangle origin x
		y: rectangle origin y!

queueDrawTexture: aDrawTexture
	| texture destinationRectangle |
	texture := aDrawTexture texture.
	texture ifNil: [^self].
	destinationRectangle := aDrawTexture stretch
				ifTrue: [aDrawTexture rectangle]
				ifFalse: 
					[| textureExtent |
					textureExtent := texture extent.
					Rectangle origin: aDrawTexture rectangle origin extent: textureExtent].
	sdlRenderer
		renderTexture: texture
		source: nil
		destination: destinationRectangle!

queueFillPolygon: aFillPolygon
	| vertices indices |
	vertices := aFillPolygon vertices collect: [:each | each asSDLVertex].
	indices := aFillPolygon indices.
	indices
		ifNil: [sdlRenderer renderGeometry: nil vertices: vertices]
		ifNotNil: 
			[sdlRenderer
				renderGeometry: nil
				vertices: vertices
				indices: (indices collect: [:each | each asSDword])]!

queueFillRectangle: aFillRectangle
	aFillRectangle color ifNotNil: [:aColor | sdlRenderer setDrawColor: aColor].
	aFillRectangle rectangle ifNotNil: [:aRectangle | sdlRenderer fillRectangle: aRectangle]!

releaseTexture: aSDL_Texture 
	aSDL_Texture destroy!

resetClip
	sdlRenderer resetClip!

sdlRenderer
	^sdlRenderer!

sdlRenderer: aSDL_Renderer
	sdlRenderer := aSDL_Renderer!

setClip: aRectangle
	sdlRenderer setClip: aRectangle!

setDrawColor: aColor
	sdlRenderer setDrawColor: aColor!

setRenderTarget: aSDLTexture
	sdlRenderer setRenderTarget: aSDLTexture!

updateTexture: aTexture pixels: anExternalAddress extent: aPoint 
	aTexture updatePixels: anExternalAddress pitch: aPoint x * 4!

viewport
	^Rectangle origin: 0 @ 0 extent: sdlRenderer getWindow getExtent!

wrapExternalTexture: anExternalHandle extent: aPoint
	| properties |
	properties := SDL_Properties new.
	properties
		setPointerProperty: SDL_PROP_TEXTURE_CREATE_D3D11_TEXTURE_POINTER value: anExternalHandle;
		setNumberProperty: SDL_PROP_TEXTURE_CREATE_WIDTH_NUMBER value: aPoint x truncated;
		setNumberProperty: SDL_PROP_TEXTURE_CREATE_HEIGHT_NUMBER value: aPoint y truncated.
	^sdlRenderer createTextureWithProperties: properties! !

!Renderer2D categoriesForMethods!
clear!public! !
clear:!public! !
createStaticTexture:!public! !
createStreamingTexture:!public! !
createTargetTexture:!public! !
createTextEngine!accessing!private! !
createTexture:!public! !
createTexture:format:!public! !
createTextureFromSurface:!public! !
destroy!public! !
direct3DDevice!public! !
downloadTexture:pixelFormat:!images!public! !
present:!public! !
queueDrawCommand:!private! !
queueDrawLine:!executing!private! !
queueDrawPoint:!private! !
queueDrawRectangle:!executing!private! !
queueDrawString:!private! !
queueDrawSurface:!executing!private! !
queueDrawText:!private! !
queueDrawTexture:!executing!private! !
queueFillPolygon:!executing!private! !
queueFillRectangle:!executing!private! !
releaseTexture:!public! !
resetClip!public! !
sdlRenderer!accessing!private! !
sdlRenderer:!accessing!private! !
setClip:!public! !
setDrawColor:!executing!public! !
setRenderTarget:!public! !
updateTexture:pixels:extent:!public! !
viewport!public! !
wrapExternalTexture:extent:!public! !
!

!Renderer2D class methodsFor!

forWindow: aWindow
	| sdlRenderer |
	sdlRenderer := aWindow createRenderer.
	sdlRenderer setDrawBlendMode: SDL_BLENDMODE_BLEND.
	^self new
		sdlRenderer: sdlRenderer;
		createTextEngine! !

!Renderer2D class categoriesForMethods!
forWindow:!public! !
!

